import "std";
import "math";

fun A(double i, double j) > double {
    double ij = i + j - 1.0;
    return 1.0 / (ij * (ij - 1.0) * 0.5 + i);
}

fun Av([double] x, [double] y, int N) > void {
    for (double i = 0.0; i < N; i = i + 1.0) {
        double a = 0.0;
        for (double j = 0.0; j < N; j = j + 1.0) {
            a = a + x[std\toInt(j)] * A(i: i, j: j);
        }
        y[std\toInt(i)] = a;
    }
}

fun Atv([double] x, [double] y, int N) > void {
    for (double i = 0.0; i < N; i = i + 1.0) {
        double a = 0.0;
        for (double j = 0.0; j < N; j = j + 1.0) {
            a = a + x[std\toInt(j)] * A(i: j, j: i);
        }
        y[std\toInt(i)] = a;
    }
}

fun AtAv([double] x, [double] y, [double] t, int N) > void {
    Av(x: x, y: t, N: N);
    Atv(x: t, y: y, N: N);
}

fun main([str] args) > void {
    int N = 100;
    if (args.len() > 0) {
        N = std\parseInt(args[0]) ?? 100;
    }

    [double] u = [<double>];
    [double] v = [<double>];
    [double] t = [<double>];
    for (int i = 0; i < N; i = i + 1) {
        u.append(1.0);
        v.append(0.0);
        t.append(0.0);
    }

    for (int i = 0; i < 10; i = i + 1) {
        AtAv(x: u, y: v, t: t, N: N);
        AtAv(x: v, y: u, t: t, N: N);
    }

    double vBv = 0.0;
    double vv = 0.0;
    for (int i = 0; i < N; i = i + 1) {
        double ui = u[i];
        double vi = v[i];
        vBv = vBv + ui * vi;
        vv = vv + vi * vi;
    }

    std\print("{math\sqrt(vBv / vv)}");
}