import "std";

protocol Comparable {
    fun greater(than: Comparable) > bool;
}

protocol Nameable {
    fun rename(name: str) > void;
}

object(Comparable, Nameable) Person {
    name: str,
    age: int,

    fun greater(than: Comparable) > bool !> str {
        if (than as other: Person) {
            return this.age > other.age;
        }

        throw "Can't compare to {than}";
    }

    fun rename(name: str) > void {
        this.name = name;
    }
}

object(Nameable) Pet {
    name: str,

    fun rename(name: str) > void {
        this.name = name;
    }
}

test "Protocols" {
    const joe = Person{
        name = "Joe",
        age = 25,
    };

    const bob = Person{
        name = "Bob",
        age = 56,
    };

    const bandit = Pet{
        name = "bandit",
    };

    std\assert(bob.greater(joe), message: "could implement protocol");

    var nameable: Nameable = bandit;
    nameable = joe;

    const nameables: [Nameable] = [bandit, joe];
    const newNames = ["Chili", "Nick"];
    foreach (i, item in nameables) {
        item.rename(newNames[i]);
    }

    std\assert(bandit.name == "Chili", message: "could call protocol method");
    std\assert(joe.name == "Nick", message: "could call protocol method");

    const map: {Nameable: bool} = {
        bandit: true,
        joe: false,
    };

    std\assert(map[joe] == false, message: "could hold protocol as map key");

    const mapValue: {str: Nameable} = {
        "bandit": bandit,
        "joe": joe,
    };

    std\assert(mapValue["joe"] == joe, message: "could hold protocol as map value");
}
